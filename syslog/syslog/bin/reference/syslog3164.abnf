SYSLOG-3164     = PRI HEADER MSG / PRI MSG / MSG;
PRI             = LESS-THAN PRIVAL GREATER-THAN;
PRIVAL          = ZERO / NONZERO-DIGIT 1*2DIGIT ;
HEADER          = TIMESTAMP HOSTNAME;
HOSTNAME        = 1*255PRINTUSASCII SP;
TIMESTAMP       = MON SP DD SP HR ":" MIN ":" SEC SP;
MON             = "Jan" / "Feb" / "Mar" / "Apr" / "May" / "Jun" / "Jul" / "Aug" / "Sep" / "Oct" / "Nov" / "Dec";
DD              = SP NONZERO-DIGIT / %d49-50 DIGIT / %d51 %d48-49;
HR              = ZERO DIGIT / ONE DIGIT / TWO ZERO-THREE;
MIN             = ZERO-FIVE DIGIT;
SEC             = ZERO-FIVE DIGIT;
MSG             = *OCTET; 
OCTET         = %d00-255 ;
SP            = %d32 ;
PRINTUSASCII  = %d33-126 ;
NONZERO-DIGIT = %d49-57 ;
ZERO          = %d48;
ONE           = %d49;
TWO           = %d50;
ZERO-THREE    = %d48-51;
ZERO-FIVE     = %d48-53;
DIGIT         = %d48 / NONZERO-DIGIT ;
LESS-THAN     = %d60;
GREATER-THAN  = %d62;
