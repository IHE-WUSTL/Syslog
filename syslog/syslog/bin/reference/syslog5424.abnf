SYSLOG-MSG      = HEADER SP STRUCTURED-DATA [SP MSG];
HEADER          = PRI VERSION SP TIMESTAMP SP HOSTNAME SP APP-NAME SP PROCID SP MSGID;
PRI             = "<" PRIVAL ">";
PRIVAL          = 1*3DIGIT ;
VERSION         = NONZERO-DIGIT 0*2DIGIT ;
HOSTNAME        = NILVALUE / 1*255PRINTUSASCII ;
APP-NAME        = NILVALUE / 1*48PRINTUSASCII ;
PROCID          = NILVALUE / 1*128PRINTUSASCII ;
MSGID           = NILVALUE / 1*32PRINTUSASCII ;
TIMESTAMP       = NILVALUE / FULL-DATE "T" FULL-TIME ;
FULL-DATE       = DATE-FULLYEAR "-" DATE-MONTH "-" DATE-MDAY ;
DATE-FULLYEAR   = 4DIGIT ;
DATE-MONTH      = 2DIGIT ; 
DATE-MDAY       = 2DIGIT ;
FULL-TIME       = PARTIAL-TIME TIME-OFFSET ;
PARTIAL-TIME    = TIME-HOUR ":" TIME-MINUTE ":" TIME-SECOND [TIME-SECFRAC] ;
TIME-HOUR       = 2DIGIT ; 
TIME-MINUTE     = 2DIGIT ;
TIME-SECOND     = 2DIGIT ;
TIME-SECFRAC    = "." 1*6DIGIT ;
TIME-OFFSET     = "Z" / TIME-NUMOFFSET ;
TIME-NUMOFFSET  = ("+" / "-") TIME-HOUR ":" TIME-MINUTE ;
STRUCTURED-DATA = NILVALUE / 1*SD-ELEMENT ;
SD-ELEMENT      = "[" SD-ID *(SP SD-PARAM) "]" ;
SD-PARAM        = PARAM-NAME "=" %d34 PARAM-VALUE %d34 ;
SD-ID           = SD-NAME ;
PARAM-NAME      = SD-NAME ;
PARAM-VALUE     = UTF-8-STRING ;
SD-NAME         = 1*32PRINTUSASCII ; 
MSG             = MSG-ANY / MSG-UTF8 ;
MSG-ANY         = *OCTET ; 
MSG-UTF8        = BOM UTF-8-STRING ;
BOM             = %xEF.BB.BF ;
UTF-8-STRING  = *OCTET ; 
OCTET         = %d00-255 ;
SP            = %d32 ;
PRINTUSASCII  = %d33-126 ;
NONZERO-DIGIT = %d49-57 ;
DIGIT         = %d48 / NONZERO-DIGIT ;
NILVALUE      = "-" ;
